# Agents Observe
# Usage: just <recipe>
#
# AGENTS_OBSERVE_SERVER_PORT & AGENTS_OBSERVE_DEV_CLIENT_PORT are read from .env
# Allows for overriding the default ports
# Server port is used by the server; the client port is only for local dev

set dotenv-load := true
set export := true
set quiet := true

port := env("AGENTS_OBSERVE_SERVER_PORT", "4981")
dev_client_port := env("AGENTS_OBSERVE_DEV_CLIENT_PORT", "5174")
project_root := justfile_directory()
server := project_root / "app" / "server"
client := project_root / "app" / "client"
cli_script := project_root / "hooks" / "scripts" / "observe_cli.mjs"
codex_hooks_script := project_root / "scripts" / "codex-hooks.mjs"

# List available recipes
default:
    @just --list

# ─── Server ─────────────────────────────────────────────

# Start the supervised collector (the same path the plugin's hooks use)
start:
    node {{ cli_script }} start
    @just open

# Start the server in the foreground (installs deps, builds the client)
start-foreground:
    npm run start

# Stop server
stop:
    node {{ cli_script }} stop

# Restart server
restart:
    node {{ cli_script }} restart

# Tail the collector server log
logs *args:
    node {{ cli_script }} logs-server {{ args }}

# ─── Development ─────────────────────────────────────────

# Start local server + client in dev mode (hot reload)
dev:
    AGENTS_OBSERVE_RUNTIME=dev AGENTS_OBSERVE_SHUTDOWN_DELAY_MS=${AGENTS_OBSERVE_SHUTDOWN_DELAY_MS:-0} node {{ project_root }}/start.mjs --skip-install

# ─── Testing ────────────────────────────────────────────

# Run all tests (server + client)
test:
    npm test

# Send a test event to the server
test-event:
    @echo '{"session_id":"test-1234","hook_event_name":"SessionStart","cwd":"/tmp","source":"new"}' \
      | AGENTS_OBSERVE_PROJECT_NAME=test-project node {{ project_root }}/hooks/scripts/observe_cli.mjs hook
    @echo "Event sent"

# ─── Database ────────────────────────────────────────────

# Delete the events database (stops server, deletes, restarts)
db-reset:
    node {{ cli_script }} db-reset

# ─── Utilities ───────────────────────────────────────────

# Check server health
health:
    node {{ cli_script }} health

# Run the CLI with a command (hook, health, start, stop, restart)
cli *args:
    node {{ cli_script }} {{ args }}

# Install user-level Codex hooks in ~/.codex/hooks.json
codex-hooks-install *args:
    node {{ codex_hooks_script }} install {{ args }}

# Check the user-level Codex hook installation
codex-hooks-status *args:
    node {{ codex_hooks_script }} status {{ args }}

# Remove only Agents Observe entries from the user-level Codex hooks file
codex-hooks-uninstall *args:
    node {{ codex_hooks_script }} uninstall {{ args }}

# Open the dashboard in browser
open port=port:
    open http://localhost:{{ port }}

# Run all tests + format (run before every commit)
check:
    npm test
    npm run fmt
    cd app/client && npm install && npm run build

# Show client bundle size visualizer in browser
bundle-visualizer:
    cd app/client && npx vite-bundle-visualizer

# Format all source files
fmt:
    npm run fmt

# Tag and push a release (bumps versions, tests, builds, tags, pushes)
release version:
    {{ project_root }}/scripts/release.sh {{ version }}

# Install all dependencies
install:
    npm install
    cd {{ server }} && npm install
    cd {{ client }} && npm install
